use num::rational::Ratio;

use ast::*;
use value::{Value, FunDef};
use op::OpCode;
use lexer::Token;

grammar;

extern {
    enum Token {
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "->" => Token::RightArrow,
        "<-" => Token::LeftArrow,
        "=>" => Token::RightFullArrow,
        "=" => Token::Equals,
        "-" => Token::Minus,
        "not" => Token::Not,
        "is" => Token::Op(OpCode::Equals),
        "isnt" => Token::Op(OpCode::Nequals),
        "<"  => Token::Op(OpCode::Lt),
        "<=" => Token::Op(OpCode::Lte),
        ">"  => Token::Op(OpCode::Gt),
        ">=" => Token::Op(OpCode::Gte),
        "and" => Token::Op(OpCode::And),
        "or" => Token::Op(OpCode::Or),
        "xor" => Token::Op(OpCode::Xor),
        "+" => Token::Op(OpCode::Add),
        "*" => Token::Op(OpCode::Mul),
        "/" => Token::Op(OpCode::Div),
        "^" => Token::Op(OpCode::Pow),
        "." => Token::Dot,
        "," => Token::Comma,
        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "|" => Token::Alternate,
        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,
        "global" => Token::Global,
        "local" => Token::Local,
        "var" => Token::Var,
        "none" => Token::None,
        boolean => Token::Bool(<bool>),
        num => Token::Num(<Ratio<i32>>),
        string => Token::Str(<String>),
        identifier => Token::Id(<String>),
        END => Token::End
    }
}

// a separated list with custom separator and optional trailing end separator
sep_list<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

// like sep_list, but needs at least 1 item with optional trailing separator
sep_list_plus<T, S>: Vec<T> = {
    <v:(<T> S)+> <e: T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

// comma separated list of values with optional trailing comma
comma<T>: Vec<T> = sep_list<T, ",">;

// comma separated list of values with optional trailing comma and at least 1
// item
comma_plus<T>: Vec<T> = sep_list_plus<T, ",">;

// expression tier, used for operator precedence
tier<Op, NextTier>: Expr = {
    <lhs: tier<Op, NextTier>> <op: Op> <rhs: NextTier> =>
        Expr::BinOp(op, Box::new(lhs), Box::new(rhs)),
    NextTier
};

comp_expr: Expr =
    <lhs: expr1> <op: operator_comp> <rhs: expr1> =>
        Expr::BinOp(op, Box::new(lhs), Box::new(rhs));

map_pair: (Expr, Expr) = {
    <a: expr> ":" <b: expr> => (a, b),
};

// without END lalrpop ignores rest of the input if we found a rule match
pub line: Expr = {
    <expr> END => <>,
};

expr: Expr = {
    expr0 => <>,
    // if structure
    "if" <cond: expr> "then" <then_part: expr> "else" <else_part: expr> =>
        Expr::Ctrl(Control::new_if(cond, then_part, else_part)),
};

expr0: Expr = {
    // right-associative
    <lhs: lvalue> "=" <rhs: expr> => Expr::Assign(lhs, Box::new(rhs)),
    <cmp: comp_expr> => cmp,
    // function call
    <e: expr1> "(" <args: comma<expr1>> ")" => Expr::Call(
        CallExpr::new(e, args, vec!())
    ),
    expr1 => <>,
};

// precedence levels
expr1: Expr = tier<operator_1, expr2>;
expr2: Expr = tier<operator_2, expr3>;
expr3: Expr = tier<operator_3, expr4>;

expr4: Expr = {
    // right-associative
    <lhs: expr5> "^" <rhs: expr4> =>
        Expr::BinOp(OpCode::Pow, Box::new(lhs), Box::new(rhs)),
    expr5 => <>,
};

expr5: Expr = {
    "-" <e: expr6> =>
        Expr::BinOp(OpCode::Neg, Box::new(e), Box::new(Expr::Empty)),
    "not" <e: expr6> =>
        Expr::BinOp(OpCode::Not, Box::new(e), Box::new(Expr::Empty)),
    expr6 => <>,
};

expr6: Expr = {
    value => <>,
};

alt_item: (Expr, Expr) = {
    <map_pair> => <>,
    <expr> => (<>, Expr::Val(1.0f32.into())),
};

value: Expr = {
    term => Expr::Val(<>),
    lvalue => Expr::LVal(<>),
    "[" <comma<expr>> "]" => Expr::List(<>),
    "[" ":" ","? "]" => Expr::Map(vec![].into_iter().collect()), // empty map
    "[" <comma_plus<map_pair>> "]" => Expr::Map(<>.into_iter().collect()),
    "[" "|" "]" => Expr::Distribution(vec![].into_iter().collect()),
    "[" <a: alt_item> "|" <list: sep_list<alt_item, "|">> "]" => {
        let mut v = list;
        v.push(a);
        Expr::Distribution(v.into_iter().collect())
    },
    // function definition
    "{" <args: identifier*> ";" <body: expr> "}" =>
        Expr::Val(Value::Func(FunDef {
            arg_names: args,
            body: Box::new(body),
        })),
    "(" <expr> ")" => <>,
};

lval_item: Expr = {
    identifier => Expr::Val(Value::Str(<>)),
    term => Expr::Val(<>),
};

lvalue: LValue = {
    <vis: (<lvalue_visibility> ".")?> <root: identifier>
        <trail: ("." <lval_item>)*> =>
        LValue::new(vis, root, trail),
};

lvalue_visibility: LValVis = {
    "global" => LValVis::Global,
    "local" => LValVis::Local,
};

term: Value = {
    "none" => Value::None,
    boolean => Value::Bool(<>),
    num => Value::Num(<>),
    string => Value::new_string(&<>),
};

operator_comp: OpCode = {
    "is" => OpCode::Equals,
    "isnt" => OpCode::Nequals,
    "<" => OpCode::Lt,
    "<=" => OpCode::Lte,
    ">" => OpCode::Gt,
    ">=" => OpCode::Gte,
};

operator_1: OpCode = {
    "and" => OpCode::And,
    "or" => OpCode::Or,
    "xor" => OpCode::Xor,
};

operator_2: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
};

operator_3: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
};
